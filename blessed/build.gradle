apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

group 'com.github.weliem'
version '0.63'

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
        events = ['FAILED', 'SKIPPED', 'PASSED']
        showStandardStreams = true
    }
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    }
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacoco {
    toolVersion = "0.8.6"
    reportsDirectory = file("$buildDir/reports")
}

jacocoTestReport {
    group = "build"
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        csv.required = false
        html.required = true
        html.outputLocation = file("${buildDir}/jacocoHtml")
    }
}

dependencies {
    implementation 'com.github.hypfvieh:dbus-java-core:4.3.2'
    implementation 'org.jetbrains:annotations:22.0.0'
    testImplementation 'org.mockito:mockito-core:4.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.3.1'
    testImplementation 'org.mockito:mockito-inline:4.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}


